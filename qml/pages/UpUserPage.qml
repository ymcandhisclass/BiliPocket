import QtQuick 2.12
import BiliPlugin 1.0
import "../components" as Components
import ".."

Rectangle {
    id: upPage
    width: parent ? parent.width : 320
    height: parent ? parent.height : 170
    color: Theme.bgPrimary

    property var controller: null
    property var rootRef: null
    property var upMid: 0
    property var upFromViewAid: 0
    property bool upVideosRequested: false
    property bool upSeasonsRequested: false
    property bool upInfoRequested: false
    property bool upInfoReady: false
    property bool requestScheduled: false
    property int skeletonPaintToken: 0
    property bool locatingLastWatched: false
    property int locatingLastWatchedFetches: 0
    property bool locatingLastWatchedAroundRequested: false
    property int locatedLastWatchedIndex: -1
    property bool upSearchMode: false
    property string upSearchKeyword: ""
    property real savedUpSearchContentX: 0
    property int upVideoPreloadWindow: 5
    property bool instantReadyTransition: false
    readonly property bool upContentReady: upInfoReady && controller && controller.upUserMid === Number(upMid) && controller.upUserName.length > 0
    readonly property bool upDynamicMode: controller && controller.upSelectedDynamic

    signal backClicked()
    signal videoSelected(string bvid)
    signal dynamicSelected(var dynamicData)

    function clampScrollState() {
        Qt.callLater(function() {
            var maxY = Math.max(0, mainFlick.contentHeight - mainFlick.height)
            if (mainFlick.contentY > maxY) mainFlick.contentY = maxY
            if (mainFlick.contentY < 0) mainFlick.contentY = 0
        })
    }

    function resetScrollState() {
        Qt.callLater(function() {
            mainFlick.contentY = 0
            filterFlick.contentX = 0
            upVideoList.contentX = 0
            clampScrollState()
        })
    }

    function resetListState() {
        locatingLastWatched = false
        locatingLastWatchedFetches = 0
        locatingLastWatchedAroundRequested = false
        locatedLastWatchedIndex = -1
        Qt.callLater(function() {
            upVideoList.contentX = 0
            clampScrollState()
        })
    }

    function scrollToVideoSection() {
        Qt.callLater(function() {
            mainFlick.contentY = Math.max(0, contentColumn.y + videoSection.y - Theme.spacingSmall)
            clampScrollState()
        })
    }

    function restoreUpSearchPosition() {
        if (!upSearchMode || savedUpSearchContentX <= 0) return
        Qt.callLater(function() {
            upSearchResultList.contentX = savedUpSearchContentX
            Qt.callLater(function() {
                upSearchResultList.contentX = savedUpSearchContentX
            })
        })
    }

    function upFansMedalLabel() {
        if (!controller || !controller.upFansMedalName) return ""
        var levelText = controller.upFansMedalLevel > 0 ? " Lv" + controller.upFansMedalLevel : ""
        return controller.upFansMedalName + levelText
    }

    function hasUpBadges() {
        return !!(controller && (controller.upOfficialLabel || controller.upVipLabel || upFansMedalLabel()))
    }

    function doUpSearch() {
        var kw = upSearchKeyword.trim()
        if (kw.length === 0) return
        var model = controller && controller.up ? controller.up.upSearchVideoModel() : null
        if (model && model.keyword === kw && model.count > 0) {
            upSearchMode = true
            scrollToVideoSection()
            restoreUpSearchPosition()
            return
        }
        savedUpSearchContentX = 0
        if (upSearchResultList) upSearchResultList.contentX = 0
        upSearchKeyword = kw
        upSearchMode = true
        scrollToVideoSection()
        if (controller && controller.up) controller.up.searchUpVideos(Number(upMid), kw, 1, 20)
    }

    function resetUpSearchState() {
        upSearchMode = false
        upSearchKeyword = ""
        savedUpSearchContentX = 0
        if (controller && controller.up && controller.up.clearUpSearch) controller.up.clearUpSearch()
    }

    function scheduleUpVideoPreload() {
        if (!visible || !controller || !controller.videoDetailPreloadEnabled) return
        if (!controller.video || !controller.video.preloadVideoDetail) return
        if (upDynamicMode) return
        upVideoPreloadTimer.restart()
    }

    function firstVisibleVideoIndex(listView) {
        if (!listView || listView.count <= 0) return 0
        listView.forceLayout()
        var idx = listView.indexAt(Math.max(0, listView.contentX + 8), Math.max(1, listView.height / 2))
        if (idx < 0) {
            idx = Math.floor(Math.max(0, listView.contentX) / Math.max(1, Theme.cardWidth + listView.spacing))
        }
        return Math.max(0, Math.min(idx, listView.count - 1))
    }

    function preloadVideosFromList(listView) {
        if (!listView || !listView.visible || listView.count <= 0) return
        if (!controller || !controller.video || !controller.video.preloadVideoDetail) return
        var modelObj = listView.model
        if (!modelObj || !modelObj.bvidAt) return
        var first = firstVisibleVideoIndex(listView)
        var start = Math.max(0, first - 1)
        var end = Math.min(listView.count, first + upVideoPreloadWindow)
        for (var i = start; i < end; ++i) {
            var bvid = modelObj.bvidAt(i)
            if (bvid && bvid.length > 0) controller.video.preloadVideoDetail(bvid)
        }
    }

    function runUpVideoPreload() {
        if (!visible || !controller || !controller.videoDetailPreloadEnabled) return
        if (upSearchMode) {
            preloadVideosFromList(upSearchResultList)
        } else {
            preloadVideosFromList(upVideoList)
        }
    }

    function hasPreloadedUpInfo(midVal) {
        return !!(controller && midVal > 0 &&
                  controller.upUserMid === midVal &&
                  controller.upUserName && controller.upUserName.length > 0)
    }

    function requestUpDependentData(midVal) {
        if (!controller || !controller.up || !midVal || midVal <= 0) return

        var videoModel = controller.up.upVideoModel()
        if (!upVideosRequested && controller.upSelectedSeasonId === 0) {
            upVideosRequested = true
            if (!videoModel || (videoModel.count <= 0 && !videoModel.loading)) {
                controller.up.fetchUpVideos(midVal, 1, 20)
            }
        }

        var seasonModel = controller.up.upSeasonModel()
        if (!upSeasonsRequested) {
            upSeasonsRequested = true
            if (!seasonModel || (seasonModel.count <= 0 && !seasonModel.loading)) {
                controller.up.fetchUpSeasons(midVal)
            }
        }
    }

    function syncPreloadedUpState() {
        var midVal = Number(upMid)
        if (!hasPreloadedUpInfo(midVal)) return false
        var wasReady = upContentReady
        if (!wasReady) instantReadyTransition = true
        upInfoReady = true
        upInfoRequested = true
        requestScheduled = false
        initialFetchTimer.stop()
        requestUpDependentData(midVal)
        if (!wasReady) {
            Qt.callLater(function() {
                instantReadyTransition = false
            })
        }
        return true
    }

    function dynamicDataFromModel(m) {
        return {
            idStr: m.idStr || "",
            type: m.type || "",
            authorName: m.authorName || "",
            authorFace: m.authorFace || "",
            authorMid: m.authorMid || 0,
            pubAction: m.pubAction || "",
            pubTime: m.pubTime || "",
            pubTs: m.pubTs || 0,
            text: m.text || "",
            majorTitle: m.majorTitle || "",
            majorCover: m.majorCover || "",
            majorBvid: m.majorBvid || "",
            majorAid: m.majorAid || 0,
            majorDurationText: m.majorDurationText || "",
            pictures: m.pictures || [],
            origSummary: m.origSummary || "",
            origTitle: m.origTitle || "",
            origCover: m.origCover || "",
            origBvid: m.origBvid || "",
            origAid: m.origAid || 0,
            repostCountText: m.repostCountText || "0",
            commentCountText: m.commentCountText || "0",
            likeCountText: m.likeCountText || "0",
            commentOid: m.commentOid || "",
            commentType: m.commentType || 0,
            isVideo: m.isVideo || false,
            isImage: m.isImage || false,
            isArticle: m.isArticle || false,
            isLive: m.isLive || false,
            isForward: m.isForward || false
        }
    }

    function positionLastWatchedIndex(idx) {
        if (idx < 0 || idx >= upVideoList.count) return
        upVideoList.forceLayout()
        upVideoList.positionViewAtIndex(idx, ListView.Center)
        Qt.callLater(function() {
            if (idx <= 1 && upVideoList.maybeFetchPrevious) {
                upVideoList.maybeFetchPrevious(true)
            }
        })
    }

    function tryScrollToLastWatched() {
        if (!locatingLastWatched || !controller || !controller.up) return
        var model = controller.up.upVideoModel()
        if (!model) return

        var targetAid = Number(upFromViewAid || 0)
        var idx = -1
        if (targetAid > 0 && model.indexOfAid) {
            idx = model.indexOfAid(targetAid)
        }
        if (targetAid <= 0 && model.indexOfLastWatched) {
            idx = model.indexOfLastWatched()
            if (idx < 0 && controller.upLastWatchedRank > 0 && model.indexOfLastWatchedRank) {
                idx = model.indexOfLastWatchedRank(controller.upLastWatchedRank)
            }
            if (idx < 0 && controller.upLastWatchedRank > 0 && model.count >= controller.upLastWatchedRank) {
                idx = controller.upLastWatchedRank - 1
            }
        }
        if (idx >= 0) {
            locatedLastWatchedIndex = idx
            scrollToVideoSection()
            Qt.callLater(function() {
                Qt.callLater(function() {
                    upPage.positionLastWatchedIndex(idx)
                })
            })
            locatingLastWatched = false
            return
        }
        if (model.loading) return
        if (targetAid > 0) {
            if (!locatingLastWatchedAroundRequested && controller.up.fetchUpVideosAroundAid) {
                locatingLastWatchedAroundRequested = true
                locatingLastWatchedFetches += 1
                controller.up.fetchUpVideosAroundAid(Number(upMid), targetAid, 20)
            } else {
                locatingLastWatched = false
            }
            return
        }
        if (model.hasMore && locatingLastWatchedFetches < 12) {
            locatingLastWatchedFetches += 1
            controller.up.fetchMoreUpVideos()
        } else {
            locatingLastWatched = false
        }
    }

    function locateLastWatched() {
        if (!controller || !controller.up) return
        locatingLastWatched = true
        locatingLastWatchedFetches = 0
        locatingLastWatchedAroundRequested = false
        if (controller.upSelectedSeasonId !== 0 || controller.upSelectedDynamic) {
            controller.up.selectUpSeason(0, "", false, 0)
        }
        Qt.callLater(tryScrollToLastWatched)
    }

    function scheduleInitialFetch() {
        var midVal = Number(upMid)
        if (!visible || !controller || !midVal || midVal <= 0) return
        if (rootRef && rootRef.currentPage !== "up") return
        if (syncPreloadedUpState()) return
        if (upInfoRequested || requestScheduled) return
        requestScheduled = true
        initialFetchTimer.restart()
    }

    function performInitialFetch() {
        var midVal = Number(upMid)
        requestScheduled = false
        if (!visible || !controller || !midVal || midVal <= 0) return
        if (rootRef && rootRef.currentPage !== "up") return
        if (syncPreloadedUpState()) return
        if (upInfoRequested) return
        upInfoRequested = true
        controller.up.fetchUpInfo(midVal)
    }

    function formatFanCount(value) {
        var n = Number(value || 0)
        if (n < 10000) return String(Math.floor(n))

        var wan = n / 10000.0
        var s = wan.toFixed(2)
        s = s.replace(/\.?0+$/, "")
        return s + "万"
    }

    onUpMidChanged: {
        initialFetchTimer.stop()
        var midVal = Number(upMid)
        upVideosRequested = false
        upSeasonsRequested = false
        upInfoRequested = false
        upInfoReady = false
        requestScheduled = false
        locatingLastWatched = false
        locatingLastWatchedFetches = 0
        locatingLastWatchedAroundRequested = false
        locatedLastWatchedIndex = -1
        upSearchMode = false
        upSearchKeyword = ""
        savedUpSearchContentX = 0
        skeletonPaintToken += 1
        var hasPreloadedInfo = hasPreloadedUpInfo(midVal)
        if (controller && controller.up && !hasPreloadedInfo) {
            if (controller.up.clearUpSearch) controller.up.clearUpSearch()
            var videoModel = controller.up.upVideoModel()
            if (videoModel && videoModel.clear) videoModel.clear()
            var seasonModel = controller.up.upSeasonModel()
            if (seasonModel && seasonModel.clear) seasonModel.clear()
            var dynamicModel = controller.feed ? controller.feed.upDynamicModel() : null
            if (dynamicModel && dynamicModel.clear) dynamicModel.clear()
        }
        if (controller && controller.upUserMid === midVal &&
                (controller.upSelectedSeasonId !== 0 || controller.upSelectedDynamic)) {
            controller.up.selectUpSeason(0, "", false, 0)
            upVideosRequested = true
        }
        syncPreloadedUpState()
        resetScrollState()
        scheduleInitialFetch()
    }

    onUpFromViewAidChanged: {
        locatingLastWatched = false
        locatingLastWatchedFetches = 0
        locatingLastWatchedAroundRequested = false
        locatedLastWatchedIndex = -1
    }

    onVisibleChanged: {
        if (visible) {
            var midVal = Number(upMid)
            if (controller && controller.upUserMid !== midVal && controller.up) {
                upInfoReady = false
                var videoModel = controller.up.upVideoModel()
                if (videoModel && videoModel.clear) videoModel.clear()
                var seasonModel = controller.up.upSeasonModel()
                if (seasonModel && seasonModel.clear) seasonModel.clear()
                var dynamicModel = controller.feed ? controller.feed.upDynamicModel() : null
                if (dynamicModel && dynamicModel.clear) dynamicModel.clear()
                upPage.resetUpSearchState()
            }
            syncPreloadedUpState()
            resetScrollState()
            scheduleInitialFetch()
            scheduleUpVideoPreload()
        }
    }

    onUpSearchModeChanged: scheduleUpVideoPreload()
    onUpDynamicModeChanged: scheduleUpVideoPreload()

    Timer {
        id: initialFetchTimer
        interval: Theme.animNormal + 16
        repeat: false
        onTriggered: upPage.performInitialFetch()
    }

    Timer {
        id: upVideoPreloadTimer
        interval: 180
        repeat: false
        onTriggered: upPage.runUpVideoPreload()
    }

    Component.onCompleted: {
        var midVal = Number(upMid)
        if (controller && controller.upUserMid !== midVal && controller.up) {
            var videoModel = controller.up.upVideoModel()
            if (videoModel && videoModel.clear) videoModel.clear()
            var seasonModel = controller.up.upSeasonModel()
            if (seasonModel && seasonModel.clear) seasonModel.clear()
            var dynamicModel = controller.feed ? controller.feed.upDynamicModel() : null
            if (dynamicModel && dynamicModel.clear) dynamicModel.clear()
            upInfoReady = false
        }
        syncPreloadedUpState()
        scheduleInitialFetch()
        scheduleUpVideoPreload()
    }

    Connections {
        target: controller
        function onUpUserChanged() {
            var midVal = Number(upMid)
            if (!controller || !midVal || controller.upUserMid !== midVal) return
            upInfoReady = true
            if (!upVideosRequested) {
                upVideosRequested = true
                Qt.callLater(function() {
                    if (!controller || controller.upUserMid !== midVal) return
                    if (controller.upSelectedSeasonId === 0) {
                        controller.up.fetchUpVideos(midVal, 1, 20)
                    }
                })
            }
            if (!upSeasonsRequested) {
                upSeasonsRequested = true
                Qt.callLater(function() {
                    if (!controller || controller.upUserMid !== midVal) return
                    controller.up.fetchUpSeasons(midVal)
                })
            }
        }
    }

    Components.TitleBar {
        id: titleBar
        title: "UP 主页"
        showBack: true
        anchors.top: parent.top
        onBackClicked: upPage.backClicked()
    }

    Flickable {
        id: mainFlick
        anchors.top: titleBar.bottom
        anchors.bottom: parent.bottom
        anchors.left: parent.left
        anchors.right: parent.right
        contentHeight: Math.max(height, contentColumn.childrenRect.height + Theme.spacingLarge + Theme.spacingMedium)
        onContentHeightChanged: upPage.clampScrollState()
        boundsBehavior: Flickable.StopAtBounds
        clip: true
        visible: opacity > 0
        opacity: upPage.upContentReady ? 1 : 0
        enabled: upPage.upContentReady
        Behavior on opacity {
            enabled: !upPage.instantReadyTransition
            NumberAnimation { duration: Theme.animNormal; easing.type: Easing.OutCubic }
        }

        Column {
            id: contentColumn
            width: parent.width
            height: childrenRect.height
            spacing: Theme.spacingLarge
            anchors.horizontalCenter: parent.horizontalCenter
            anchors.top: parent.top
            anchors.topMargin: Theme.spacingLarge

            // 头像 + 信息
            Row {
                id: headerRow
                width: parent.width - Theme.spacingLarge * 2
                height: Theme.s * 64
                anchors.horizontalCenter: parent.horizontalCenter
                spacing: Theme.spacingLarge

                Rectangle {
                    id: avatarWrap
                    width: Theme.s * 64
                    height: Theme.s * 64
                    radius: Theme.s * 32
                    color: Theme.bgTertiary
                    border.color: Theme.primary
                    border.width: 2

                    // 圆形裁剪由 image provider 的 round/ 前缀完成，无需 OpacityMask
                    Image {
                        id: avatarImage
                        anchors.fill: parent
                        anchors.margins: 2
                        sourceSize: Qt.size(120, 120)
                        source: controller && controller.upUserFace
                            ? "image://bili/round/size/120x120/" + encodeURIComponent(controller.upUserFace)
                            : ""
                        fillMode: Image.PreserveAspectCrop
                        smooth: true
                        mipmap: true
                        asynchronous: true
                    }
                }

                Column {
                    id: infoColumn
                    // 64(头像) + spacing + 余量，需随 s 缩放
                    width: parent.width - Theme.s * 96
                    spacing: Theme.spacingSmall
                    anchors.verticalCenter: parent.verticalCenter

                    Row {
                        width: parent.width
                        spacing: Theme.spacingSmall

                        Text {
                            id: upNameText
                            width: parent.width - uidBadge.width - Theme.spacingSmall
                            text: controller ? controller.upUserName : ""
                            color: Theme.textPrimary
                            font.family: Theme.fontFamily
                            font.pixelSize: Theme.fontMedium
                            font.bold: true
                            elide: Text.ElideRight
                        }

                        // UID 徽标
                        Rectangle {
                            id: uidBadge
                            height: Theme.s * 18
                            radius: Theme.radiusRound
                            color: Theme.withAlpha(Theme.primary, 0.12)
                            border.color: Theme.withAlpha(Theme.primary, 0.35)
                            border.width: 1
                            width: uidText.implicitWidth + 14

                            Text {
                                id: uidText
                                anchors.centerIn: parent
                                text: "UID " + (controller ? String(controller.upUserMid) : "0")
                                color: Theme.textSecondary
                                font.family: Theme.fontFamily
                                font.pixelSize: Theme.fontSmall
                                font.bold: true
                            }
                        }
                    }

                    Row {
                        spacing: Theme.spacingSmall

                        Rectangle {
                            width: Theme.s * 40
                            height: Theme.s * 18
                            radius: Theme.radiusRound
                            color: Theme.primary

                            Text {
                                anchors.centerIn: parent
                                text: "LV" + (controller ? controller.upUserLevel : 0)
                                color: Theme.textOnPrimary
                                font.family: Theme.fontFamily
                                font.pixelSize: Theme.fontSmall
                                font.bold: true
                            }
                        }

                        Rectangle {
                            height: Theme.s * 18
                            radius: Theme.radiusRound
                            color: controller && controller.upIsFollowing
                                   ? (followArea.pressed ? Theme.bgTertiary : Theme.bgSecondary)
                                   : (followArea.pressed ? Theme.primaryDark : Theme.primary)
                            border.width: controller && controller.upIsFollowing ? 1 : 0
                            border.color: controller && controller.upIsFollowing
                                          ? Theme.withAlpha(Theme.primary, 0.35)
                                          : "transparent"
                            width: followText.implicitWidth + 16

                            Text {
                                id: followText
                                anchors.centerIn: parent
                                text: controller && controller.upIsFollowing ? "已关注" : "关注"
                                color: controller && controller.upIsFollowing ? Theme.textSecondary : Theme.textOnPrimary
                                font.family: Theme.fontFamily
                                font.pixelSize: Theme.fontSmall
                                font.bold: true
                            }

                            MouseArea {
                                id: followArea
                                anchors.fill: parent
                                onClicked: {
                                    if (controller) controller.up.toggleUpFollow()
                                }
                            }
                        }
                    }

                    Row {
                        width: parent.width
                        height: visible ? 16 : 0
                        spacing: Theme.s * 4
                        visible: upPage.hasUpBadges()
                        clip: true

                        Rectangle {
                            visible: controller && controller.upOfficialLabel !== ""
                            height: Theme.s * 16
                            radius: Theme.radiusRound
                            color: Theme.withAlpha(Theme.primary, 0.16)
                            border.color: Theme.withAlpha(Theme.primary, 0.36)
                            border.width: 1
                            width: Math.min(84, officialBadgeText.implicitWidth + 12)

                            Text {
                                id: officialBadgeText
                                anchors.centerIn: parent
                                width: parent.width - Theme.s * 8
                                text: controller ? controller.upOfficialLabel : ""
                                color: Theme.primaryLight
                                font.family: Theme.fontFamily
                                font.pixelSize: Theme.fontTiny
                                font.bold: true
                                elide: Text.ElideRight
                                horizontalAlignment: Text.AlignHCenter
                            }
                        }

                        Rectangle {
                            visible: controller && controller.upVipLabel !== ""
                            height: Theme.s * 16
                            radius: Theme.radiusRound
                            color: Theme.withAlpha(Theme.accent, 0.18)
                            border.color: Theme.withAlpha(Theme.accent, 0.38)
                            border.width: 1
                            width: Math.min(64, vipBadgeText.implicitWidth + 12)

                            Text {
                                id: vipBadgeText
                                anchors.centerIn: parent
                                width: parent.width - Theme.s * 8
                                text: controller ? controller.upVipLabel : ""
                                color: "#FB7299"
                                font.family: Theme.fontFamily
                                font.pixelSize: Theme.fontTiny
                                font.bold: true
                                elide: Text.ElideRight
                                horizontalAlignment: Text.AlignHCenter
                            }
                        }

                        Rectangle {
                            visible: upPage.upFansMedalLabel() !== ""
                            height: Theme.s * 16
                            radius: Theme.radiusRound
                            color: Theme.withAlpha(Theme.warning, 0.16)
                            border.color: Theme.withAlpha(Theme.warning, 0.36)
                            border.width: 1
                            width: Math.min(70, medalBadgeText.implicitWidth + 12)

                            Text {
                                id: medalBadgeText
                                anchors.centerIn: parent
                                width: parent.width - Theme.s * 8
                                text: upPage.upFansMedalLabel()
                                color: Theme.warning
                                font.family: Theme.fontFamily
                                font.pixelSize: Theme.fontTiny
                                font.bold: true
                                elide: Text.ElideRight
                                horizontalAlignment: Text.AlignHCenter
                            }
                        }
                    }
                }
            }

            // 个人签名（放在头像下面）
            Rectangle {
                width: parent.width - Theme.spacingLarge * 2
                anchors.horizontalCenter: parent.horizontalCenter
                radius: Theme.radiusLarge
                color: Theme.bgSecondary
                border.color: Theme.withAlpha(Theme.primary, 0.15)
                border.width: 1
                height: signText.implicitHeight + Theme.spacingMedium * 2

                Text {
                    id: signText
                    anchors.fill: parent
                    anchors.margins: Theme.spacingMedium
                    text: controller && controller.upUserSign !== "" ? controller.upUserSign : "这个人很懒，什么都没写~"
                    color: Theme.textSecondary
                    font.family: Theme.fontFamily
                    font.pixelSize: Theme.fontBody
                    wrapMode: Text.WordWrap
                }
            }

            // 粉丝/关注
            Row {
                width: parent.width - Theme.spacingLarge * 2
                height: Theme.s * 28
                anchors.horizontalCenter: parent.horizontalCenter
                spacing: Theme.spacingSmall

                readonly property real colW: (width - Theme.spacingSmall) / 2

                Column {
                    width: parent.colW
                    spacing: Theme.s * 4

                    Text {
                        width: parent.width
                        text: controller ? upPage.formatFanCount(controller.upUserFans) : "0"
                        color: Theme.textPrimary
                        font.family: Theme.fontFamily
                        font.pixelSize: Theme.fontMedium
                        font.bold: true
                        horizontalAlignment: Text.AlignHCenter
                    }
                    Text {
                        width: parent.width
                        text: "粉丝"
                        color: Theme.textSecondary
                        font.family: Theme.fontFamily
                        font.pixelSize: Theme.fontSmall
                        horizontalAlignment: Text.AlignHCenter
                    }
                }

                Column {
                    width: parent.colW
                    spacing: Theme.s * 4

                    Text {
                        width: parent.width
                        text: controller ? String(controller.upUserFollowing) : "0"
                        color: Theme.textPrimary
                        font.family: Theme.fontFamily
                        font.pixelSize: Theme.fontMedium
                        font.bold: true
                        horizontalAlignment: Text.AlignHCenter
                    }
                    Text {
                        width: parent.width
                        text: "关注"
                        color: Theme.textSecondary
                        font.family: Theme.fontFamily
                        font.pixelSize: Theme.fontSmall
                        horizontalAlignment: Text.AlignHCenter
                    }
                }
            }

            // 投稿视频 / 合集筛选
            Item {
                id: videoSection
                width: parent.width
                height: videoHeader.height + Theme.spacingSmall + filterStrip.height + Theme.spacingNormal + upVideoList.height

                // 标题行：当前筛选名 + 视频数量提示
                Item {
                    id: videoHeader
                    width: parent.width
                    height: Theme.s * 24
                    anchors.top: parent.top

                    Row {
                        anchors.left: parent.left
                        anchors.leftMargin: Theme.spacingLarge
                        anchors.verticalCenter: parent.verticalCenter
                        width: parent.width - Theme.spacingLarge * 2 - lastWatchedButton.width - searchButton.width - 12
                        spacing: Theme.spacingSmall

                        // 装饰条
                        Rectangle {
                            width: Theme.s * 3
                            height: titleHeaderText.implicitHeight - 2
                            radius: 1.5
                            color: Theme.primary
                            anchors.verticalCenter: parent.verticalCenter
                        }

                        Text {
                            id: titleHeaderText
                            text: upPage.upSearchMode
                                  ? "搜索：" + upPage.upSearchKeyword
                                  : (upPage.upDynamicMode
                                     ? "图文动态"
                                  : (controller && controller.upSelectedSeasonId !== 0
                                     ? (controller.upSelectedSeasonName || "合集")
                                     : "视频列表"))
                            color: Theme.textPrimary
                            font.family: Theme.fontFamily
                            font.pixelSize: Theme.fontMedium
                            font.bold: true
                            elide: Text.ElideRight
                            width: Math.min(implicitWidth, upPage.width - 176)
                        }

                        Text {
                            id: listCountText
                            anchors.verticalCenter: parent.verticalCenter
                            visible: controller && (upPage.upSearchMode
                                                     ? (!!upSearchResultList.model && upSearchResultList.count > 0)
                                                     : (upPage.upDynamicMode
                                                        ? (!!upDynamicList.model && upDynamicList.count > 0)
                                                        : (controller.upVideoTotal > 0 || (!!upVideoList.model && upVideoList.count > 0))))
                            text: " · " + (upPage.upSearchMode
                                           ? upSearchResultList.count + " 个视频"
                                           : (upPage.upDynamicMode
                                              ? upDynamicList.count + " 条动态"
                                              : (controller.upVideoTotal > 0 ? controller.upVideoTotal : upVideoList.count) + " 个视频"))
                            color: Theme.textTertiary
                            font.family: Theme.fontFamily
                            font.pixelSize: Theme.fontSmall
                        }
                    }

                    Rectangle {
                        id: searchButton
                        anchors.right: lastWatchedButton.left
                        anchors.rightMargin: Theme.s * 6
                        anchors.verticalCenter: parent.verticalCenter
                        visible: !upPage.upDynamicMode
                        width: visible ? 34 : 0
                        height: Theme.s * 20
                        radius: Theme.s * 10
                        color: searchArea.pressed ? Theme.withAlpha(Theme.primary, 0.22)
                                                  : Theme.withAlpha(Theme.primary, 0.10)
                        border.color: Theme.withAlpha(Theme.primary, 0.32)
                        border.width: 1

                        Canvas {
                            anchors.centerIn: parent
                            width: 13
                            height: 13
                            onPaint: {
                                var ctx = getContext("2d")
                                ctx.clearRect(0, 0, width, height)
                                ctx.strokeStyle = Theme.textSecondary
                                ctx.lineWidth = 1.6
                                ctx.beginPath()
                                ctx.arc(5.5, 5.5, 4, 0, Math.PI * 2, false)
                                ctx.stroke()
                                ctx.beginPath()
                                ctx.moveTo(8.7, 8.7)
                                ctx.lineTo(12, 12)
                                ctx.stroke()
                            }
                        }

                        MouseArea {
                            id: searchArea
                            anchors.fill: parent
                            onClicked: upSearchKeyboard.open(upPage.upSearchKeyword)
                        }
                    }

                    Rectangle {
                        id: lastWatchedButton
                        anchors.right: parent.right
                        anchors.rightMargin: Theme.spacingLarge
                        anchors.verticalCenter: parent.verticalCenter
                        visible: !upPage.upDynamicMode
                        width: visible ? 58 : 0
                        height: Theme.s * 20
                        radius: Theme.s * 10
                        color: lastWatchedArea.pressed ? Theme.withAlpha(Theme.primary, 0.22)
                                                        : Theme.withAlpha(Theme.primary, 0.10)
                        border.color: Theme.withAlpha(Theme.primary, 0.32)
                        border.width: 1
                        opacity: upPage.upSearchMode || (controller && controller.loggedIn) ? 1 : 0.55

                        Text {
                            anchors.centerIn: parent
                            text: upPage.upSearchMode ? "返回列表" : "上次观看"
                            color: Theme.textSecondary
                            font.family: Theme.fontFamily
                            font.pixelSize: Theme.fontSmall
                            font.bold: true
                        }

                        MouseArea {
                            id: lastWatchedArea
                            anchors.fill: parent
                            onClicked: {
                                if (upPage.upSearchMode) {
                                    upPage.upSearchMode = false
                                } else {
                                    upPage.locateLastWatched()
                                }
                            }
                        }
                    }
                }

                Item { width: parent.width; height: 2 }

                // 合集筛选条：水平滚动 chip 列表
                Item {
                    id: filterStrip
                    width: parent.width
                    height: Theme.s * 24
                    visible: !upPage.upSearchMode
                    anchors.top: videoHeader.bottom
                    anchors.topMargin: Theme.spacingSmall

                    Flickable {
                        id: filterFlick
                        anchors.fill: parent
                        contentWidth: filterRow.width + Theme.spacingLarge * 2
                        contentHeight: filterStrip.height
                        flickableDirection: Flickable.HorizontalFlick
                        boundsBehavior: Flickable.StopAtBounds
                        clip: true

                        // chip 条是 Flickable+Repeater 而非 ListView，照 LoadMoreListView 的 guard 手动接线
                        property bool loadingMoreSeasons: false
                        onAtXEndChanged: {
                            if (!atXEnd || !controller || !controller.up) return
                            if (contentWidth <= width + 2) return
                            var seasonModel = controller.up.upSeasonModel()
                            if (!seasonModel || seasonModel.count <= 0) return
                            if (loadingMoreSeasons || seasonModel.loading) return
                            if (seasonModel.hasMore === false) return
                            loadingMoreSeasons = true
                            controller.up.fetchMoreUpSeasons()
                        }

                        Row {
                            id: filterRow
                            x: Theme.spacingLarge
                            spacing: Theme.s * 6
                            anchors.verticalCenter: parent.verticalCenter

                            // “视频”全部 chip
                            Rectangle {
                                id: allChip
                                height: filterStrip.height
                                width: allChipText.implicitWidth + 26
                                radius: height / 2
                                property bool selected: !controller || (!controller.upSelectedDynamic && controller.upSelectedSeasonId === 0)
                                color: selected ? Theme.primary
                                                : (allChipArea.pressed ? Theme.bgTertiary : Theme.bgSecondary)
                                border.color: selected ? "transparent"
                                                       : Theme.withAlpha(Theme.primary, 0.25)
                                border.width: selected ? 0 : 1

                                Behavior on color { ColorAnimation { duration: Theme.animFast } }

                                Text {
                                    id: allChipText
                                    anchors.centerIn: parent
                                    text: "视频"
                                    color: parent.selected ? Theme.textOnPrimary : Theme.textSecondary
                                    font.family: Theme.fontFamily
                                    font.pixelSize: Theme.fontSmall
                                    font.bold: parent.selected
                                }

                                MouseArea {
                                    id: allChipArea
                                    anchors.fill: parent
                                    onClicked: {
                                        if (!controller) return
                                        if (controller.upSelectedSeasonId !== 0 || controller.upSelectedDynamic) {
                                            controller.up.selectUpSeason(0, "", false, 0)
                                            upPage.resetListState()
                                        }
                                    }
                                }
                            }

                            Rectangle {
                                id: dynamicChip
                                height: filterStrip.height
                                width: dynamicChipText.implicitWidth + 26
                                radius: height / 2
                                property bool selected: controller && controller.upSelectedDynamic
                                color: selected ? Theme.primary
                                                : (dynamicChipArea.pressed ? Theme.bgTertiary : Theme.bgSecondary)
                                border.color: selected ? "transparent"
                                                       : Theme.withAlpha(Theme.primary, 0.25)
                                border.width: selected ? 0 : 1

                                Behavior on color { ColorAnimation { duration: Theme.animFast } }

                                Row {
                                    anchors.centerIn: parent
                                    spacing: Theme.s * 4

                                    Canvas {
                                        id: dynamicChipIcon
                                        width: 10
                                        height: 10
                                        anchors.verticalCenter: parent.verticalCenter
                                        property bool selected: dynamicChip.selected
                                        onSelectedChanged: requestPaint()
                                        onPaint: {
                                            var ctx = getContext("2d")
                                            ctx.clearRect(0, 0, width, height)
                                            var c = dynamicChip.selected ? Theme.textOnPrimary : Theme.textSecondary
                                            ctx.strokeStyle = c
                                            ctx.fillStyle = c
                                            ctx.lineWidth = 1.1
                                            ctx.lineCap = "round"
                                            ctx.lineJoin = "round"

                                            ctx.beginPath()
                                            ctx.moveTo(1.2, 2)
                                            ctx.lineTo(8.8, 2)
                                            ctx.quadraticCurveTo(9.4, 2, 9.4, 2.6)
                                            ctx.lineTo(9.4, 7.8)
                                            ctx.quadraticCurveTo(9.4, 8.4, 8.8, 8.4)
                                            ctx.lineTo(1.2, 8.4)
                                            ctx.quadraticCurveTo(0.6, 8.4, 0.6, 7.8)
                                            ctx.lineTo(0.6, 2.6)
                                            ctx.quadraticCurveTo(0.6, 2, 1.2, 2)
                                            ctx.closePath()
                                            ctx.stroke()

                                            ctx.beginPath()
                                            ctx.arc(3, 4, 0.9, 0, Math.PI * 2, false)
                                            ctx.fill()

                                            ctx.beginPath()
                                            ctx.moveTo(1.2, 7.6)
                                            ctx.lineTo(3.5, 5.6)
                                            ctx.lineTo(5.1, 6.9)
                                            ctx.lineTo(6.8, 5.1)
                                            ctx.lineTo(8.8, 7.6)
                                            ctx.stroke()
                                        }
                                        Component.onCompleted: requestPaint()
                                    }

                                    Text {
                                        id: dynamicChipText
                                        anchors.verticalCenter: parent.verticalCenter
                                        text: "图文"
                                        color: dynamicChip.selected ? Theme.textOnPrimary : Theme.textSecondary
                                        font.family: Theme.fontFamily
                                        font.pixelSize: Theme.fontSmall
                                        font.bold: dynamicChip.selected
                                    }
                                }

                                MouseArea {
                                    id: dynamicChipArea
                                    anchors.fill: parent
                                    onClicked: {
                                        if (!controller || !controller.up) return
                                        if (!dynamicChip.selected) {
                                            upPage.resetUpSearchState()
                                            controller.up.selectUpDynamic()
                                            upPage.resetListState()
                                        }
                                    }
                                }
                            }

                            Repeater {
                                model: controller ? controller.up.upSeasonModel() : null

                                delegate: Rectangle {
                                    id: seasonChip
                                    height: filterStrip.height
                                    width: Math.min(seasonChipText.implicitWidth + 26, 172)
                                    radius: height / 2
                                    property bool selected: controller && !controller.upSelectedDynamic && controller.upSelectedSeasonId === model.seasonId
                                    color: selected ? Theme.primary
                                                    : (seasonChipArea.pressed ? Theme.bgTertiary : Theme.bgSecondary)
                                    border.color: selected ? "transparent"
                                                           : Theme.withAlpha(Theme.primary, 0.25)
                                    border.width: selected ? 0 : 1

                                    Behavior on color { ColorAnimation { duration: Theme.animFast } }

                                    Row {
                                        anchors.centerIn: parent
                                        spacing: Theme.s * 4

                                        // 合集小图标：两层错位的小方块，暗示叠放/合集
                                        Item {
                                            width: Theme.s * 10; height: Theme.s * 10
                                            anchors.verticalCenter: parent.verticalCenter

                                            Rectangle {
                                                width: Theme.s * 7; height: Theme.s * 7
                                                radius: 1.5
                                                color: "transparent"
                                                border.color: seasonChip.selected ? Theme.textOnPrimary : Theme.textSecondary
                                                border.width: 1
                                                x: 0; y: 3
                                            }
                                            Rectangle {
                                                width: Theme.s * 7; height: Theme.s * 7
                                                radius: 1.5
                                                color: seasonChip.selected ? Theme.textOnPrimary : Theme.textSecondary
                                                x: 3; y: 0
                                            }
                                        }

                                        Text {
                                            id: seasonChipText
                                            anchors.verticalCenter: parent.verticalCenter
                                            text: (model.name || "合集")
                                                  + (model.total > 0 ? "·" + model.total : "")
                                            color: seasonChip.selected ? Theme.textOnPrimary : Theme.textSecondary
                                            font.family: Theme.fontFamily
                                            font.pixelSize: Theme.fontSmall
                                            font.bold: seasonChip.selected
                                            elide: Text.ElideRight
                                            // chip 总宽 max 172，扣掉左右内边距与图标和间距 ≈ 140
                                            width: Math.min(implicitWidth, 140)
                                        }
                                    }

                                    MouseArea {
                                        id: seasonChipArea
                                        anchors.fill: parent
                                        onClicked: {
                                            if (!controller) return
                                            if (!seasonChip.selected) {
                                                controller.up.selectUpSeason(model.seasonId,
                                                                          model.name || "",
                                                                          model.isSeries === true,
                                                                          model.total || 0)
                                                upPage.resetListState()
                                            }
                                        }
                                    }
                                }
                            }

                            // 合集分页"加载中"尾部指示
                            Rectangle {
                                id: seasonLoadingChip
                                readonly property var seasonModel: controller && controller.up ? controller.up.upSeasonModel() : null
                                visible: !!(seasonModel && seasonModel.loading && seasonModel.count > 0)
                                height: filterStrip.height
                                width: seasonLoadingText.implicitWidth + 26
                                radius: height / 2
                                color: Theme.bgSecondary
                                border.color: Theme.withAlpha(Theme.primary, 0.25)
                                border.width: 1

                                Text {
                                    id: seasonLoadingText
                                    anchors.centerIn: parent
                                    text: "加载中…"
                                    color: Theme.textTertiary
                                    font.family: Theme.fontFamily
                                    font.pixelSize: Theme.fontSmall
                                }
                            }
                        }
                    }

                }

                Connections {
                    target: controller ? controller.up.upSeasonModel() : null
                    function onLoadingChanged() {
                        if (!target || target.loading) return
                        filterFlick.loadingMoreSeasons = false
                        upPage.clampScrollState()
                    }
                }

                Components.LoadMoreListView {
                    id: upVideoList
                    visible: !upPage.upSearchMode && !upPage.upDynamicMode
                    width: parent.width
                    height: Theme.s * 135
                    anchors.top: filterStrip.bottom
                    anchors.topMargin: Theme.spacingNormal
                    spacing: Theme.s * 6
                    model: controller ? controller.up.upVideoModel() : null
                    leftMargin: 4
                    rightMargin: 4
                    // 反向加载（“上次观看”定位后往前翻）用 _loadingPrevious，正向仍走内置 loadingMore
                    property bool _loadingPrevious: false
                    property int _previousCountBeforeLoad: 0

                    function maybeFetchPrevious(force) {
                        if (!controller || !controller.up) return
                        if (!force && !atXBeginning) return
                        if (upVideoList.contentWidth <= upVideoList.width + 2) return
                        if (upVideoList._loadingPrevious || upVideoList.loadingMore) return
                        if (upVideoList.model && upVideoList.model.loading) return
                        if (!controller.up.canFetchPreviousUpVideos || !controller.up.canFetchPreviousUpVideos()) return
                        upVideoList._loadingPrevious = true
                        upVideoList._previousCountBeforeLoad = upVideoList.count
                        controller.up.fetchPreviousUpVideos()
                    }

                    onAtXBeginningChanged: if (atXBeginning) maybeFetchPrevious()
                    onMovementEnded: {
                        maybeFetchPrevious()
                        upPage.scheduleUpVideoPreload()
                    }
                    onDraggingChanged: if (!dragging) {
                        maybeFetchPrevious()
                        upPage.scheduleUpVideoPreload()
                    }
                    onContentXChanged: upPage.scheduleUpVideoPreload()

                    onLoadMoreRequested: {
                        if (controller && controller.up) controller.up.fetchMoreUpVideos()
                    }

                    delegate: Components.VideoCardCompact {
                        height: upVideoList.height
                        videoTitle: model.title || ""
                        coverUrl: model.pic || ""
                        imageActive: upPage.visible
                        preferOffscreenPlaceholder: controller && controller.videoCardOffscreenPlaceholderEnabled
                        upName: model.ownerName || ""
                        viewCount: model.views || ""
                        durationText: model.durationText || ""
                        bvid: model.bvid || ""
                        isLastWatched: (Number(upPage.upFromViewAid || 0) > 0 && Number(model.aid || 0) === Number(upPage.upFromViewAid))
                                       || model.isLastWatchedArc === true
                                       || index === upPage.locatedLastWatchedIndex
                        partCount: model.partCount || 1
                        titleScale: 0.9
                        subScale: 0.85
                        onClicked: upPage.videoSelected(bvid)
                    }

                    Row {
                        visible: upVideoList.count === 0 && controller && controller.isLoading
                        anchors.left: parent.left
                        anchors.leftMargin: Theme.s * 4
                        anchors.verticalCenter: parent.verticalCenter
                        spacing: Theme.s * 6
                        Repeater {
                            model: 3
                            Components.VideoCardCompact {
                                height: upVideoList.height
                                placeholder: true
                                        titleScale: 0.9
                                subScale: 0.85
                            }
                        }
                    }
                }

                Components.LoadMoreListView {
                    id: upDynamicList
                    visible: !upPage.upSearchMode && upPage.upDynamicMode
                    width: parent.width
                    height: Theme.s * 135
                    anchors.top: filterStrip.bottom
                    anchors.topMargin: Theme.spacingNormal
                    orientation: ListView.Vertical
                    spacing: Theme.s * 5
                    // 纵向小视口：需覆盖横向默认的 cacheBuffer/displayMargin
                    cacheBuffer: 360
                    displayMarginBeginning: 120
                    displayMarginEnd: Theme.listDisplayMargin
                    model: controller && controller.feed ? controller.feed.upDynamicModel() : null
                    leftMargin: 6
                    rightMargin: 6
                    topMargin: 1
                    bottomMargin: 1

                    delegate: Components.DynamicFeedCard {
                        width: upDynamicList.width - 12
                        authorName: model.authorName || ""
                        authorFace: model.authorFace || ""
                        pubAction: model.pubAction || ""
                        pubTime: model.pubTime || ""
                        text: model.text || ""
                        majorTitle: model.majorTitle || ""
                        majorCover: model.majorCover || ""
                        majorBvid: model.majorBvid || ""
                        majorDurationText: model.majorDurationText || ""
                        pictures: model.pictures || []
                        origSummary: model.origSummary || ""
                        origTitle: model.origTitle || ""
                        origCover: model.origCover || ""
                        repostCountText: model.repostCountText || "0"
                        commentCountText: model.commentCountText || "0"
                        likeCountText: model.likeCountText || "0"
                        isVideo: model.isVideo || false
                        isImage: model.isImage || false
                        isArticle: model.isArticle || false
                        isLive: model.isLive || false
                        isForward: model.isForward || false
                        imageActive: upPage.visible && upPage.upDynamicMode
                        onClicked: {
                            if (bvid && bvid.length > 0) {
                                upPage.videoSelected(bvid)
                            } else {
                                upPage.dynamicSelected(upPage.dynamicDataFromModel(model))
                            }
                        }
                    }

                    onLoadMoreRequested: {
                        if (controller && controller.feed) controller.feed.fetchMoreUpDynamics()
                    }
                }

                Components.LoadMoreListView {
                    id: upSearchResultList
                    visible: upPage.upSearchMode
                    width: parent.width
                    height: Theme.s * 135
                    anchors.top: videoHeader.bottom
                    anchors.topMargin: Theme.spacingSmall
                    spacing: Theme.s * 6
                    model: controller && controller.up ? controller.up.upSearchVideoModel() : null
                    leftMargin: 4
                    rightMargin: 4

                    delegate: Components.VideoCardCompact {
                        height: upSearchResultList.height
                        videoTitle: model.title || ""
                        coverUrl: model.pic || ""
                        imageActive: upPage.visible && upPage.upSearchMode
                        preferOffscreenPlaceholder: controller && controller.videoCardOffscreenPlaceholderEnabled
                        upName: model.ownerName || ""
                        viewCount: model.views || ""
                        durationText: model.durationText || ""
                        bvid: model.bvid || ""
                        partCount: model.partCount || 1
                        titleScale: 0.9
                        subScale: 0.85
                        onClicked: {
                            upPage.savedUpSearchContentX = upSearchResultList.contentX
                            upPage.videoSelected(bvid)
                        }
                    }

                    Row {
                        visible: upPage.upSearchMode
                                 && upSearchResultList.model
                                 && upSearchResultList.model.loading
                                 && upSearchResultList.count === 0
                        anchors.left: parent.left
                        anchors.leftMargin: Theme.s * 4
                        anchors.verticalCenter: parent.verticalCenter
                        spacing: Theme.s * 6
                        Repeater {
                            model: 3
                            Components.VideoCardCompact {
                                height: upSearchResultList.height
                                placeholder: true
                                        titleScale: 0.9
                                subScale: 0.85
                            }
                        }
                    }

                    onLoadMoreRequested: {
                        if (controller && controller.up) controller.up.searchMoreUpVideos()
                    }

                    onCountChanged: {
                        upPage.restoreUpSearchPosition()
                        upPage.scheduleUpVideoPreload()
                    }

                    onContentXChanged: upPage.scheduleUpVideoPreload()
                    onMovementEnded: upPage.scheduleUpVideoPreload()
                    onDraggingChanged: if (!dragging) upPage.scheduleUpVideoPreload()
                }

                Connections {
                    target: upVideoList.model
                    function onLoadingChanged() {
                        if (!target || target.loading) return
                        upVideoList._loadingPrevious = false
                        upPage.clampScrollState()
                        upPage.tryScrollToLastWatched()
                        upPage.scheduleUpVideoPreload()
                    }
                    function onCountChanged() {
                        if (upVideoList._loadingPrevious) {
                            // prepend 后补偿滚动位置：每张卡片占位 = 卡宽 + spacing
                            var added = Math.max(0, upVideoList.count - upVideoList._previousCountBeforeLoad)
                            if (added > 0) {
                                upVideoList.contentX += added * (Theme.cardWidth + upVideoList.spacing)
                            }
                        }
                        upPage.tryScrollToLastWatched()
                        upPage.scheduleUpVideoPreload()
                    }
                }

                Connections {
                    target: upDynamicList.model
                    function onLoadingChanged() {
                        if (!target || target.loading) return
                        upPage.clampScrollState()
                    }
                }

                Connections {
                    target: upSearchResultList.model
                    function onLoadingChanged() {
                        if (!target || target.loading) return
                        upPage.restoreUpSearchPosition()
                        upPage.scheduleUpVideoPreload()
                    }
                }

                Text {
                    visible: !upPage.upSearchMode && !upPage.upDynamicMode
                             && upVideoList.count === 0 && controller && !controller.isLoading
                    text: controller && controller.upSelectedSeasonId !== 0 ? "该合集暂无视频" : "暂无投稿"
                    color: Theme.textTertiary
                    anchors.horizontalCenter: parent.horizontalCenter
                    anchors.top: upVideoList.bottom
                    anchors.topMargin: Theme.s * 6
                }

                Text {
                    visible: !upPage.upSearchMode && upPage.upDynamicMode
                             && upDynamicList.count === 0
                             && upDynamicList.model
                             && !upDynamicList.model.loading
                    text: upDynamicList.model && upDynamicList.model.errorMessage
                          ? upDynamicList.model.errorMessage
                          : "暂无图文动态"
                    color: Theme.textTertiary
                    anchors.horizontalCenter: parent.horizontalCenter
                    anchors.top: upDynamicList.bottom
                    anchors.topMargin: Theme.s * 6
                    font.family: Theme.fontFamily
                    font.pixelSize: Theme.fontSmall
                }

                Text {
                    visible: upPage.upSearchMode
                             && upSearchResultList.count === 0
                             && upSearchResultList.model
                             && !upSearchResultList.model.loading
                    text: upSearchResultList.model && upSearchResultList.model.errorMessage
                          ? upSearchResultList.model.errorMessage
                          : "未找到相关视频"
                    color: Theme.textTertiary
                    anchors.horizontalCenter: parent.horizontalCenter
                    anchors.top: upSearchResultList.bottom
                    anchors.topMargin: Theme.s * 6
                    font.family: Theme.fontFamily
                    font.pixelSize: Theme.fontSmall
                }
            }
        }
    }

    Components.VirtualKeyboardInput {
        id: upSearchKeyboard
        objectName: "from_UpUserPage.qml"
        onAccepted: {
            upPage.upSearchKeyword = content.trim()
            if (upPage.upSearchKeyword.length > 0) {
                upPage.doUpSearch()
            }
        }
    }

    Item {
        id: upSkeletonLayer
        anchors.top: titleBar.bottom
        anchors.bottom: parent.bottom
        anchors.left: parent.left
        anchors.right: parent.right
        visible: opacity > 0
        opacity: upPage.upContentReady ? 0 : 1
        z: 20
        clip: true
        Behavior on opacity {
            enabled: !upPage.instantReadyTransition
            NumberAnimation { duration: Theme.animNormal; easing.type: Easing.OutCubic }
        }

        Column {
            width: parent.width
            spacing: Theme.spacingLarge
            anchors.horizontalCenter: parent.horizontalCenter
            anchors.top: parent.top
            anchors.topMargin: Theme.spacingLarge

            Row {
                width: parent.width - Theme.spacingLarge * 2
                height: Theme.s * 64
                anchors.horizontalCenter: parent.horizontalCenter
                spacing: Theme.spacingLarge

                Canvas {
                    width: 64
                    height: 64
                    property int paintToken: upPage.skeletonPaintToken
                    Component.onCompleted: requestPaint()
                    onPaintTokenChanged: requestPaint()
                    onVisibleChanged: if (visible) requestPaint()
                    onWidthChanged: requestPaint()
                    onHeightChanged: requestPaint()
                    onPaint: {
                        var ctx = getContext("2d")
                        ctx.clearRect(0, 0, width, height)
                        ctx.fillStyle = Theme.bgTertiary
                        ctx.beginPath()
                        ctx.arc(width / 2, height / 2, 31, 0, Math.PI * 2, false)
                        ctx.fill()
                        ctx.strokeStyle = Theme.withAlpha(Theme.primary, 0.38)
                        ctx.lineWidth = 2
                        ctx.stroke()
                        ctx.strokeStyle = Theme.withAlpha(Theme.textSecondary, 0.22)
                        ctx.lineWidth = 2
                        ctx.beginPath()
                        ctx.arc(32, 25, 8, 0, Math.PI * 2, false)
                        ctx.stroke()
                        ctx.beginPath()
                        ctx.arc(32, 47, 15, Math.PI * 1.05, Math.PI * 1.95, false)
                        ctx.stroke()
                    }
                }

                Components.SkeletonPill {
                    width: parent.width - Theme.s * 96
                    height: Theme.s * 58
                    anchors.verticalCenter: parent.verticalCenter
                    paintToken: upPage.skeletonPaintToken
                    // x/w ≤1 为宽度比例，>1 为绝对像素
                    pills: [
                        { x: 0, y: 4, w: 0.72, h: 12, color: Theme.withAlpha(Theme.textSecondary, 0.22) },
                        { x: 0.76, y: 4, w: 0.22, h: 12, color: Theme.withAlpha(Theme.primary, 0.16) },
                        { x: 0, y: 30, w: 40, h: 18, color: Theme.withAlpha(Theme.primary, 0.20) },
                        { x: 48, y: 30, w: 58, h: 18, color: Theme.withAlpha(Theme.textSecondary, 0.16) }
                    ]
                }
            }

            Rectangle {
                width: parent.width - Theme.spacingLarge * 2
                height: Theme.s * 44
                anchors.horizontalCenter: parent.horizontalCenter
                radius: Theme.s * 10
                color: Theme.bgSecondary

                Components.SkeletonPill {
                    anchors.fill: parent
                    paintToken: upPage.skeletonPaintToken
                    pills: [
                        { x: 12, y: 11, w: 0.78, h: 8, color: Theme.withAlpha(Theme.textSecondary, 0.18) },
                        { x: 12, y: 26, w: 0.46, h: 8, color: Theme.withAlpha(Theme.textSecondary, 0.13) }
                    ]
                }
            }

            Components.SkeletonPill {
                width: parent.width - Theme.spacingLarge * 2
                height: Theme.s * 32
                anchors.horizontalCenter: parent.horizontalCenter
                paintToken: upPage.skeletonPaintToken
                pills: [
                    { x: 0, y: 0, w: 0.26, h: 10, color: Theme.withAlpha(Theme.textPrimary, 0.18) },
                    { x: 0, y: 18, w: 52, h: 14, color: Theme.withAlpha(Theme.primary, 0.20) },
                    { x: 60, y: 18, w: 78, h: 14, color: Theme.withAlpha(Theme.textSecondary, 0.14) },
                    { x: 146, y: 18, w: 68, h: 14, color: Theme.withAlpha(Theme.textSecondary, 0.12) }
                ]
            }

            Row {
                width: parent.width
                height: Theme.s * 135
                spacing: Theme.s * 6
                anchors.horizontalCenter: parent.horizontalCenter
                Repeater {
                    model: 3
                    Components.VideoCardCompact {
                        height: Theme.s * 135
                        placeholder: true
                        titleScale: 0.9
                        subScale: 0.85
                    }
                }
            }
        }
    }
}
